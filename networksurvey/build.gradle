apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"

// Exclude google-services and crashlytics if the json file is missing and for debug builds
if (file("google-services.json").exists() && !gradle.startParameter.taskNames.any { it.contains("Debug") || it.contains("test") }) {
    logger.quiet("Using the google-services.json file")
    apply plugin: "com.google.gms.google-services"
    apply plugin: "com.google.firebase.crashlytics"
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'


ext.androidVersion = '28.0.0'
ext.grpcVersion = '1.65.1'
ext.networkSurveyMessagingVersion = '1.5.0'
ext.navigationVersion = '2.7.7'

android {
    compileSdk 34
    defaultConfig {
        buildConfigField "String", "MESSAGING_API_VERSION", "\"${networkSurveyMessagingVersion}\""
        buildConfigField 'String', "NS_API_KEY", "\"${nsApiKey}\""
        applicationId "com.craxiom.networksurvey"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 85
        versionName "1.30.2"
        setProperty("archivesBaseName", "$applicationName-$versionName")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    buildTypes {
        debug {
            applicationIdSuffix ".dev" // Suffix for debug builds
            resValue "string", "app_name", "NS - dev"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    buildFeatures {
        viewBinding true
        dataBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.7'
    }
    kotlinOptions {
        jvmTarget = "17"
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties', 'google/cloud/audit/audit_log.proto']
        }
    }

    dependenciesInfo {
        // Disables dependency metadata when building APKs.
        includeInApk = false
    }

    lint {
        abortOnError false
    }
    namespace 'com.craxiom.networksurvey'

    flavorDimensions "version"
    productFlavors {
        // Regular is for Google Play that contains Firebase tracking features such as crashlytics
        regular {
            versionNameSuffix ""
        }

        // Has the extra CDR features and does not contain any tracking
        cdr {
            versionNameSuffix ""
        }
    }
}

configurations.configureEach {
    exclude group: 'com.google.android.gms', module: 'play-services-ads'
    exclude group: 'com.google.android.gms', module: 'play-services-ads-identifier'
    exclude group: 'com.google.android.gms', module: 'play-services-ads-lite'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation "androidx.navigation:navigation-runtime-ktx:${navigationVersion}"
    implementation "androidx.navigation:navigation-fragment-ktx:${navigationVersion}"
    implementation "androidx.navigation:navigation-ui-ktx:${navigationVersion}"
    implementation "androidx.navigation:navigation-compose:${navigationVersion}"
    implementation "androidx.viewpager2:viewpager2:1.1.0"
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation("io.grpc:grpc-protobuf:${grpcVersion}") {
        exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
    }

    implementation "io.grpc:grpc-okhttp:${grpcVersion}"
    implementation "io.grpc:grpc-android:${grpcVersion}"

    implementation("com.craxiom:network-survey-messaging:${networkSurveyMessagingVersion}") {
        exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
    }
    implementation "com.google.protobuf:protobuf-java-util:4.27.3"

    implementation "mil.nga.geopackage:geopackage-android:6.7.3"
    implementation 'org.apache.commons:commons-csv:1.10.0'

    implementation 'com.craxiom:mqtt-library:0.7.3'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'

    implementation 'app.futured.donut:donut:2.2.3'
    implementation 'com.github.MackHartley:RoundedProgressBar:3.0.0'
    implementation 'com.patrykandpatrick.vico:core:2.0.0-alpha.6'
    implementation 'com.patrykandpatrick.vico:compose-m3:2.0.0-alpha.6'

    implementation 'com.github.yuriy-budiyev:code-scanner:2.3.2'
    implementation 'com.google.zxing:core:3.5.3'

    implementation 'com.google.dagger:dagger:2.51.1'
    kapt 'com.google.dagger:dagger-compiler:2.51.1'
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'org.osmdroid:osmdroid-android:6.1.18'
    // Provides additional features including Marker clustering
    implementation 'com.github.MKergall:osmbonuspack:6.9.0'

    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2024.08.00')
    implementation(composeBom)
    androidTestImplementation(composeBom)

    implementation "androidx.compose.runtime:runtime"
    implementation "androidx.compose.ui:ui"
    implementation "androidx.compose.foundation:foundation"
    implementation "androidx.compose.foundation:foundation-layout"
    implementation "androidx.compose.material3:material3"
    implementation "androidx.compose.runtime:runtime-livedata"
    implementation "androidx.compose.ui:ui-tooling"
    implementation "androidx.compose.ui:ui-tooling-preview"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose"
    implementation "androidx.lifecycle:lifecycle-runtime-compose"
    implementation 'androidx.compose.ui:ui-tooling:1.6.8'
    implementation "com.google.accompanist:accompanist-themeadapter-material:0.28.0"

    // Only include firebase in the google play build
    regularImplementation platform('com.google.firebase:firebase-bom:33.1.2')
    regularImplementation 'com.google.firebase:firebase-analytics'
    regularImplementation 'com.google.firebase:firebase-crashlytics'

    testImplementation 'junit:junit:4.13.2'
    implementation 'androidx.annotation:annotation:1.8.2'

    // Uncomment if you want to hunt for memory leaks
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:3.0-alpha-1'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'com.adevinta.android:barista:4.2.0'
    androidTestImplementation 'com.google.truth:truth:1.1.2'
    androidTestImplementation "com.craxiom:network-survey-messaging:${networkSurveyMessagingVersion}"
    androidTestImplementation "mil.nga.geopackage:geopackage-android:6.7.3"
}
